#스크립트 제작 : 킬봇 (https://www.twitch.tv/killbot460/about)
#사용된 애드온 : SkBee (https://forums.skunity.com/resources/skbee.976)

command /돈 [<text>]:
	trigger:
		if arg-1 is not set:
			message "돈 : %{bank.%player%.money}%원"
		else:
			if player is not op:
				message "돈 : %{bank.%player%.money}%원"
			else:
				message "돈 : %{bank.%arg-1%.money}%원"

command /은행 [<text>] [<text>] [<text>]:
	trigger:	
		if {bank.%player%.trading} is true:
			set {bank.%player%.trading} to false
			message "<pink>진행 중이던 거래가 취소되었습니다."
		else if arg-1 is "입금":
			if arg-2 is not set:
				message "<pink>입금액을 입력해 주세요."
			else if arg-2 parsed as integer is not set:
				message "<pink>올바른 입금액을 입력해 주세요."
			else if arg-2 parsed as integer is less than 0:
				message "<pink>올바른 입금액을 입력해 주세요."
			else if {bank.%player%.money} is less than arg-2 parsed as integer:
				message "<pink>보유하신 돈이<r> <bold>%arg-2%<r><pink>원 보다 적습니다."
			else:
				add arg-2 parsed as integer to {bank.%player%.balance}
				remove arg-2 parsed as integer from {bank.%player%.money}
				BankLogging("deposit",player,"none",arg-2)
				message "<bold>%arg-2%<r> <lime>원이 입금되었습니다. | 잔고 :<r> <bold>%{bank.%player%.balance}%<r><lime>원"
		else if arg-1 is "출금":
			if arg-2 is not set:
				message "<pink>출금액을 입력해 주세요."
			else if arg-2 parsed as integer is not set:
				message "<pink>올바른 출금액을 입력해 주세요."
			else if arg-2 parsed as integer is less than 0:
				message "<pink>올바른 출금액을 입력해 주세요."
			else if {bank.%player%.balance} is less than arg-2 parsed as integer:
				message "<pink>잔고가<r> <bold>%arg-2%<r><pink>원 보다 적습니다."
			else:
				add arg-2 parsed as integer to {bank.%player%.money}
				remove arg-2 parsed as integer from {bank.%player%.balance}
				BankLogging("withdraw",player,"none",arg-2)
				message "<bold>%arg-2%<r> <lime>원이 출금되었습니다. | 잔고 :<r> <bold>%{bank.%player%.balance}%<r><lime>원"
		else if arg-1 is "송금":
			if arg-2 is not set:
				message "<pink>송금할 대상을 입력해 주세요."
			else if arg-3 is not set:
				message "<pink>송금액을 입력해 주세요."
			else if arg-3 parsed as integer is not set:
				message "<pink>올바른 송금액을 입력해 주세요."
			else if arg-3 parsed as integer is less than 0:
				message "<pink>올바른 송금액을 입력해 주세요."
			else if {bank.%player%.balance} is less than arg-3 parsed as integer:
				message "<pink>잔고가<r> <bold>%arg-3%<r><pink>원 보다 적습니다."
			else:
				set {bank.%player%.trading} to true
				message "<bold>%arg-2%<r> 님에게 <bold>%arg-3%<r>원을 송금합니다."
				set {_text::1} to text component from "[예]"
				set {_text::2} to text component from " / "
				set {_text::3} to text component from "[아니오]"
				set click event of {_text::1} to click event to run command "/banksend %arg-2% %arg-3%"
				set click event of {_text::3} to click event to run command "/bankcancel"
				send components {_text::*} to player			
		else if arg-1 is "설정":
			if player is not op:
				message "<pink>관리자만 사용 가능한 명령어입니다."
			else if arg-2 is not set:
				message "<pink>대상을 입력해 주세요."
			else if arg-3 is not set:
				message "<pink>설정액을 입력해 주세요."
			else if arg-3 parsed as integer is not set:
				message "<pink>올바른 설정액을 입력해 주세요."
			else if arg-3 parsed as integer is less than 0:
				message "<pink>올바른 설정액을 입력해 주세요."
			else:
				set {bank.%player%.trading} to true
				message "<bold><pink>%arg-2%님의 통장을 %arg-3%원으로 설정합니다. 확실합니까?"
				set {_text::1} to text component from "[예]"
				set {_text::2} to text component from " / "
				set {_text::3} to text component from "[아니오]"
				set click event of {_text::1} to click event to run command "/bankset %arg-2% %arg-3%"
				set click event of {_text::3} to click event to run command "/bankcancel"
				send components {_text::*} to player			
		else if arg-1 is "추가":
			if player is not op:
				message "<pink>관리자만 사용 가능한 명령어입니다."
			else if arg-2 is not set:
				message "<pink>대상을 입력해 주세요."
			else if arg-3 is not set:
				message "<pink>추가액을 입력해 주세요."
			else if arg-3 parsed as integer is not set:
				message "<pink>올바른 추가액을 입력해 주세요."
			else if arg-3 parsed as integer is less than 0:
				message "<pink>올바른 추가액을 입력해 주세요."
			else:
				set {bank.%player%.trading} to true
				message "<bold><pink>%arg-2%님의 통장에 %arg-3%원을 추가합니다. 확실합니까?"
				set {_text::1} to text component from "[예]"
				set {_text::2} to text component from " / "
				set {_text::3} to text component from "[아니오]"
				set click event of {_text::1} to click event to run command "/bankadd %arg-2% %arg-3%"
				set click event of {_text::3} to click event to run command "/bankcancel"
				send components {_text::*} to player
		else if arg-1 is "제거":
			if player is not op:
				message "<pink>관리자만 사용 가능한 명령어입니다."
			else if arg-2 is not set:
				message "<pink>대상을 입력해 주세요."
			else if arg-3 is not set:
				message "<pink>제거액을 입력해 주세요."
			else if arg-3 parsed as integer is not set:
				message "<pink>올바른 제거액을 입력해 주세요."
			else if arg-3 parsed as integer is less than 0:
				message "<pink>올바른 제거액을 입력해 주세요."
			else:
				set {bank.%player%.trading} to true
				message "<bold><pink>%arg-2%님의 통장에서 %arg-3%원을 제거합니다. 확실합니까?"
				set {_text::1} to text component from "[예]"
				set {_text::2} to text component from " / "
				set {_text::3} to text component from "[아니오]"
				set click event of {_text::1} to click event to run command "/bankremove %arg-2% %arg-3%"
				set click event of {_text::3} to click event to run command "/bankcancel"
				send components {_text::*} to player
		else if arg-1 is "잔고":	
			if player is not op:
				message "잔고 : %{bank.%player%.balance}%원"
			else if arg-2 is not set:
				message "잔고 : %{bank.%player%.balance}%원"
			else:
				message "%arg-2% 님의 잔고 : %{bank.%arg-2%.balance}%원"
		else if arg-1 is "통장":
			if arg-2 is not set:
				BankBook(player,1)
			else if arg-2 parsed as integer is not set:
				message "<pink>올바른 페이지를 입력해 주세요."
			else if arg-2 parsed as integer is less than 1:
				message "<pink>올바른 페이지를 입력해 주세요."
			else:
				set {_page} to arg 2 parsed as number
				BankBook(player,{_page})
		else if arg-1 is "내역":
			if player is not op:
				message "<pink>관리자만 사용 가능한 명령어입니다."
			else if arg-3 is not set:
				BankLog(player,arg-2,1)
			else if arg-3 parsed as integer is not set:
				message "<pink>올바른 페이지를 입력해 주세요."
			else if arg-3 parsed as integer is less than 1:
				message "<pink>올바른 페이지를 입력해 주세요."
			else:
				set {_page} to arg 3 parsed as number
				BankLog(player,arg-2,{_page})
		else:
			message ""
			message "---------------------------------------------------"
			message ""
			message "<yellow>버튼을 클릭하여 은행을 사용할 수 있습니다."
			message ""
			message "<sgt:/은행 입금 ><ttp:/은행 입금 [금액]><lime>[입금]<r> - 통장에 금액을 입금합니다.       <sgt:/은행 출금 ><ttp:/은행 출금 [금액]><lime>[출금]<r> - 통장에서 금액을 출금합니다."
			message "<sgt:/은행 송금 ><ttp:/은행 송금 [플레이어] [금액]><lime>[송금]<r> - 플레이어에게 금액을 송금합니다. <sgt:/은행 통장><ttp:/은행 통장 [페이지]><lime>[통장]<r> - 거래 내역을 확인합니다."
			message ""
			message "------------------------------------  <cmd:/bankhelp><italic><underlined>도움이 필요하신가요?"
			message ""

command /bankhelp:
	trigger:
		message "<lime>명령어 사용법:"
		message "/은행 입금 [금액]               - 통장에 금액을 입금합니다."
		message "/은행 츨금 [금액]               - 통장에서 금액을 출금합니다."
		message "/은행 송금 [플레이어] [금액]    - 통장에서 금액을 출금합니다."
		message "/은행 통장 [페이지]             - 거래 내역을 확인합니다."
		if player is op:
			message "<yellow>관리자 명령어:"
			message "/은행 설정 [플레이어] [금액]    - 플레이어의 잔고를 금액으로 설정합니다."
			message "/은행 추가 [플레이어] [금액]    - 플레이어의 통장에 금액을 추가합니다."
			message "/은행 제거 [플레이어] [금액]    - 플레이어의 통장에서 금액을 제거합니다."
			message "/은행 내역 [플레이어] [페이지]  - 플레이어의 거래 내역을 확인합니다."

command /banksend [<text>] [<text>]:
	trigger:
		if {bank.%player%.trading} is true:
			add arg-2 parsed as integer to {bank.%arg-1%.balance}
			remove arg-2 parsed as integer from {bank.%player%.balance}
			set {_time} to now formatted as "yyyy.MM.dd HH:mm:ss"
			BankLogging("send",player,arg-1,arg-2)
			message "<lime>%arg-1%님에게<r> <bold>%arg-2%<r><lime>원을 송금했습니다."
			set {bank.%player%.trading} to false
		else:
			message "<pink>잘못된 접근입니다."

command /bankset [<text>] [<text>]:
	trigger:
		if player is not op:
			message "<pink> 관리자만 사용 가능한 명령어입니다."
			set {bank.%player%.trading} to false
		else if {bank.%player%.trading} is true:
			set {bank.%arg-1%.balance} to arg-2 parsed as integer
			message "<lime>%arg-1%님의 잔고를<r> <bold>%arg-2%<r><lime>원으로 설정했습니다."
			BankLogging("set",player,arg-1,arg-2)
			set {bank.%player%.trading} to false
		else:
			message "<pink>잘못된 접근입니다."

command /bankadd [<text>] [<text>]:
	trigger:
		if player is not op:
			message "<pink> 관리자만 사용 가능한 명령어입니다."
			set {bank.%player%.trading} to false
		else if {bank.%player%.trading} is true:
			add arg-2 parsed as integer to {bank.%arg-1%.balance}
			message "<lime>%arg-1%님의 통장에<r> <bold>%arg-2%<r><lime>원을 추가했습니다."
			BankLogging("add",player,arg-1,arg-2)
			set {bank.%player%.trading} to false
		else:
			message "<pink>잘못된 접근입니다."

command /bankremove [<text>] [<text>]:
	trigger:
		if player is not op:
			message "<pink>관리자만 사용 가능한 명령어입니다."
			set {bank.%player%.trading} to false
		else if {bank.%player%.trading} is true:
			remove arg-2 parsed as integer from {bank.%arg-1%.balance}
			message "<lime>%arg-1%님의 통장에서<r> <bold>%arg-2%<r><lime>원을 제거했습니다."
			BankLogging("remove",player,arg-1,arg-2)
			set {bank.%player%.trading} to false
		else:
			message "<pink>잘못된 접근입니다."

command /bankcancel:
	trigger:
		if {bank.%player%.trading} is true:
			set {bank.%player%.trading} to false
			message "<pink>거래가 취소되었습니다."
		else:
			message "<pink>진행중인 거래가 없습니다."

function BankLog(send: player, player: text, page: number):	
	loop 2147483647 times:
		if {bank.%{_player}%.log::%loop-number%} is not set:
			exit 1 loop
		else:
			set {_max} to loop-number
	if {_max} is not set:
		message "<pink>내역이 없습니다." to {_send}
	else:
		remove (({_page} * 10) - 1) from {_max}
		loop 10 times:
			if {bank.%{_player}%.log::%{_max}%} is not set:
				message "비어 있음" to {_send}
				add 1 to {_max}
			else:
				message "%{bank.%{_player}%.log::%{_max}%}%" to {_send}
				add 1 to {_max}
		set {_text::1} to text component from "[<]"
		set {_text::2} to text component from " %{_page}% 페이지 "
		set {_text::3} to text component from "[>]"
		set click event of {_text::1} to click event to run command "/은행 내역 %{_player}% %({_page} + 1)%"
		set click event of {_text::3} to click event to run command "/은행 내역 %{_player}% %({_page} - 1)%"
		if {_page} is less than 2:
			send components {_text::1} and {_text::2} to {_send}
		else:
			send components {_text::*} to {_send}

function BankBook(player: player, page: number):
	loop 2147483647 times:
		if {bank.%{_player}%.log::%loop-number%} is not set:
			exit 1 loop	
		else:
			set {_max} to loop-number
	if {_max} is not set:
		message "<pink>내역이 없습니다." to {_player}
	else:
		remove (({_page} * 10) - 1) from {_max}
		loop 10 times:
			if {bank.%{_player}%.log::%{_max}%} is not set:
				message "비어 있음" to {_player}
				add 1 to {_max}
			else:
				message "%{bank.%{_player}%.log::%{_max}%}%" to {_player}
				add 1 to {_max}
		set {_text::1} to text component from "[<]"
		set {_text::2} to text component from " %{_page}% 페이지 "
		set {_text::3} to text component from "[>]"
		set click event of {_text::1} to click event to run command "/은행 통장 %({_page} + 1)%"
		set click event of {_text::3} to click event to run command "/은행 통장 %({_page} - 1)%"
		if {_page} is less than 2:
			send components {_text::1} and {_text::2} to {_player}
		else:
			send components {_text::*} to {_player}

function BankLogging(text: text, player1: player, player2: text, value: text):
	set {_time} to now formatted as "yyyy.MM.dd HH:mm:ss"
	loop 2147483647 times:
		if {bank.%{_player1}%.log::%loop-number%} is not set:
			if {_text} is "send":
				set {bank.%{_player1}%.log::%loop-number%} to "<pink>%{_player1}% : %{_player2}% 님에게 %{_value}%원 송금"
				set {bank.%{_player1}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player1}%.balance}%원 [%{_time}%]"
				exit 1 loop
			else if {_text} is "set":
				set {bank.%{_player1}%.log::%loop-number%} to "<yellow>%{_player1}% : %{_player2}%님의 통장을 %{_value}%원으로 설정"
				exit 1 loop
			else if {_text} is "add":
				set {bank.%{_player1}%.log::%loop-number%} to "<yellow>%{_player1}% : %{_player2}% 님에게 %{_value}%원 추가"
				exit 1 loop				
			else if {_text} is "remove":
				set {bank.%{_player1}%.log::%loop-number%} to "<yellow>%{_player1}% : %{_player2}% 님에게 %{_value}%원 제거"
				exit 1 loop
			else if {_text} is "deposit":
				set {bank.%{_player1}%.log::%loop-number%} to "<lime>%{_player1}% : %{_value}% 원 입금"
				set {bank.%{_player1}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player1}%.balance}%원 [%{_time}%]"
				exit 1 loop
			else if {_text} is "withdraw":
				set {bank.%{_player1}%.log::%loop-number%} to "<pink>%{_player1}% : %{_value}% 원 출금"
				set {bank.%{_player1}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player1}%.balance}%원 [%{_time}%]"
				exit 1 loop
	if {_player2} is not "none":
		loop 2147483647 times:
			if {bank.%{_player2}%.log::%loop-number%} is not set:
				if {_text} is "send":
					set {bank.%{_player2}%.log::%loop-number%} to "<lime>%{_player2}% : %{_player1}% 님이 %{_value}% 원 송금" 
					set {bank.%{_player2}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player2}%.balance}%원 [%{_time}%]" 
					exit 1 loop
				else if {_text} is "set":
					set {bank.%{_player2}%.log::%loop-number%} to "<yellow>%{_player2}% : %{_player1}%님이 통장을 %{_value}%원으로 설정" 
					set {bank.%{_player2}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player2}%.balance}%원 [%{_time}%]" 
					exit 1 loop
				else if {_text} is "add":
					set {bank.%{_player2}%.log::%loop-number%} to "<yellow>%{_player2}% : %{_player1}% 님이 %{_value}% 원 추가" 
					set {bank.%{_player2}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player2}%.balance}%원 [%{_time}%]" 
					exit 1 loop
				else if {_text} is "remove":
					set {bank.%{_player2}%.log::%loop-number%} to "<yellow>%{_player2}% : %{_player1}% 님이 %{_value}% 원 제거" 
					set {bank.%{_player2}%.log::%(loop-number + 1 )%} to "잔고 : %{bank.%{_player2}%.balance}%원 [%{_time}%]" 
					exit 1 loop